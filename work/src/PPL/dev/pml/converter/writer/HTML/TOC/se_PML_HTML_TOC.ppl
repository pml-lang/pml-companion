// SPDX-License-Identifier: GPL-2.0-only
// Copyright (C) 2018 - 2021 Christian Neumanns, email: chris@pml-lang.dev

service PML_HTML_TOC

    function write_TOC
        in document_node PML_document_node
        in context PML_HTML_writer_context
        in max_level pos_32 default:10

        const chapters = i_document_node.chapters on_null: return

        write_TOC_start ( context, document_node )

        repeat for each chapter in chapters
            write_TOC_chapter ( chapter, i_context, i_max_level )
        .

        write_TOC_end ( context )
    .

    functions access:private

        function write_TOC_start ( context PML_HTML_writer_context, document_node PML_document_node )

            context.write_HTML_block_tag_start ( HTML_tag_name = "nav", CSS_class_name = "pml-toc" )
            context.increase_indent

            if i_document_node.TOC_title as TOC_title is not null then
                context.write_HTML_tag_start ( write_indent = yes, HTML_tag_name = "h2", CSS_class_name = "pml-toc-title",
                    append_new_line = no )

                context.write_escaped_HTML ( TOC_title )

                context.write_HTML_inline_tag_end ( "h2" )
                context.write_new_line
            .

            context.write_HTML_tag_start ( write_indent = yes, HTML_tag_name = "div", CSS_class_name = "pml-toc-tree",
                attributes = [ "id": "TOCTree" ], append_new_line = yes )
            context.increase_indent

            context.write_HTML_block_tag_start ( HTML_tag_name = "ul", CSS_class_name = null )
            context.increase_indent
        .

        function write_TOC_end ( context PML_HTML_writer_context )

            context.decrease_indent
            context.write_HTML_block_tag_end ( "ul" )
            context.decrease_indent
            context.write_HTML_block_tag_end ( "div" )
            context.decrease_indent
            context.write_HTML_block_tag_end ( "nav" )
        .

        function write_TOC_chapter
            in chapter PML_chapter_node
            in context PML_HTML_writer_context
            in max_level pos_32 default:10

            const is_leaf = not chapter.has_sub_chapters or \
                chapter.level >= max_level

            const id = i_context.escape_HTML ( chapter.node_id )
            const title = i_context.escape_HTML ( chapter.title )

            if is_leaf then
                context.write_indent
                context.write_HTML_inline_tag_start ( HTML_tag_name = "li", CSS_class_name = "pml-toc-leaf-node" )

                context.write_HTML_tag_start ( write_indent = no, HTML_tag_name = "a",
                    CSS_class_name = null, attributes = [ "href": """#{{id}}""" ], append_new_line = no )

                context.write ( title )

                context.write_HTML_inline_tag_end ( "a" )
                context.write_HTML_inline_tag_end ( "li" )

                context.write_new_line


            else
                context.write_HTML_block_tag_start ( HTML_tag_name = "li", CSS_class_name = "pml-toc-branch-node" )
                context.increase_indent

                context.write_HTML_block_tag_start ( HTML_tag_name = "details", CSS_class_name = null )
                context.increase_indent

                context.write_indent
                context.write_HTML_inline_tag_start ( HTML_tag_name = "summary", CSS_class_name = null )

                context.write_HTML_tag_start ( write_indent = no, HTML_tag_name = "a",
                    CSS_class_name = null, attributes = [ "href": """#{{id}}""" ], append_new_line = no )

                context.write ( title )

                context.write_HTML_inline_tag_end ( "a" )
                context.write_HTML_inline_tag_end ( "summary" )
                context.write_new_line

                context.write_HTML_block_tag_start ( HTML_tag_name = "ul", CSS_class_name = null )
                context.increase_indent

                if chapter.sub_chapters as sub_chapters is not null then
                    repeat for each sub_chapter in sub_chapters
                        write_TOC_chapter ( chapter = sub_chapter, i_context, i_max_level )
                    .
                .

                context.decrease_indent
                context.write_HTML_block_tag_end ( "ul" )

                context.decrease_indent
                context.write_HTML_block_tag_end ( "details" )

                context.decrease_indent
                context.write_HTML_block_tag_end ( "li" )
            .
        .
    .
.

// SPDX-License-Identifier: GPL-2.0-only
// Copyright (C) 2018 - 2021 Christian Neumanns, email: chris@pml-lang.dev

factory PML_insert_source_code_node_creator type:PML_node_creator<PML_insert_source_code_node>

    function create_node

        const file_path = i_context.get_attribute_value<absolute_or_relative_file_path> ( se_PML_attribute_ids.file_parameter_id )
        const file = file.create_for_absolute_or_relative_path_in_working_directory ( file_path )
        const code = define_code ( file, i_context ) on_error: return_error

        return PML_insert_source_code_node.create (
            node_id = i_context.get_id_or_null,
            file,
            code,
            language = i_context.get_string_attribute ( se_PML_source_code_formal_node.language_parameter.standard_id )
            use_highlighter = i_context.get_yes_no_attribute ( se_PML_source_code_formal_node.use_highlighter_parameter.standard_id )
            HTML_attributes = i_context.HTML_attributes )
    .

    functions access:private

        function define_code -> string or anticipated_error
            in file file
            // in node_AST PML_node_AST
            // in attributes any_type_parameters
            in context PML_node_creator_context

            const file_code = se_text_file_reader.try_read_non_empty_content ( file ) on_error: return_error

            const from_regex = i_context.get_attribute_value<regex or null> (
                se_PML_insert_source_code_formal_node.from_regex_parameter.standard_id )
            const to_regex = i_context.get_attribute_value<regex or null> (
                se_PML_insert_source_code_formal_node.to_regex_parameter.standard_id )

            if from_regex =r null and to_regex =r null then
                return file_code
            .

            variable code = file_code

            if from_regex is not null then

                const regex_match_result = code.find_first_regex ( from_regex )
                if regex_match_result is null then
                    i_context.non_canceling_error (
                        id = "REGEX_NOT_FOUND_IN_SOURCE_CODE"
                        info = """Regex '{{from_regex}}' could not be found in file '{{file.to_string}}'"""
                        token = i_context.pXML_node.attributes?.get_or_null ( se_PML_insert_source_code_formal_node.from_regex_parameter.standard_id.value )?.value )
                    return code
                .

                const include_from_regex = i_context.get_yes_no_attribute (
                    se_PML_insert_source_code_formal_node.include_from_regex_parameter.standard_id )
                const start_index = \
                    if include_from_regex \
                    then regex_match_result.start_index \
                    else regex_match_result.end_index + 1
                if start_index ># code.size then
                    i_context.non_canceling_error (
                        id = "REGEX_NOT_FOUND_IN_SOURCE_CODE"
                        info = """There is no more code after regex '{{from_regex}}' in file '{{file.to_string}}'"""
                        token = i_context.pXML_node.attributes?.get_or_null ( se_PML_insert_source_code_formal_node.from_regex_parameter.standard_id.value )?.value )
                    return code
                .

                code = code.substring_from ( start_index )
            .

            if to_regex is not null then

                const regex_match_result = code.find_first_regex ( to_regex )
                if regex_match_result is null then
                    i_context.non_canceling_error (
                        id = "REGEX_NOT_FOUND_IN_SOURCE_CODE"
                        info = """Regex '{{to_regex}}' could not be found in file '{{file.to_string}}'"""
                        token = i_context.pXML_node.attributes?.get_or_null ( se_PML_insert_source_code_formal_node.to_regex_parameter.standard_id.value )?.value )
                    return code
                .

                const include_to_regex = i_context.get_yes_no_attribute (
                    se_PML_insert_source_code_formal_node.include_to_regex_parameter.standard_id )
                const end_index = \
                    if include_to_regex \
                    then regex_match_result.end_index \
                    else regex_match_result.start_index - 1
                if end_index <= 0 then
                    i_context.non_canceling_error (
                        id = "REGEX_NOT_FOUND_IN_SOURCE_CODE"
                        info = """There is no code before regex '{{to_regex}}' in file '{{file.to_string}}'"""
                        token = i_context.pXML_node.attributes?.get_or_null ( se_PML_insert_source_code_formal_node.to_regex_parameter.standard_id.value )?.value )
                    return code
                .

                code = code.substring_to ( end_index )
            .

            return i_context.remove_smallest_indent_in_all_lines_of_text ( code )
        .
    .

    creator create kind:in_all
.

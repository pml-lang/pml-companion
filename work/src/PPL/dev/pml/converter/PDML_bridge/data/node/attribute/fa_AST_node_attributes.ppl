// SPDX-License-Identifier: GPL-2.0-only
// Copyright (C) 2018 - 2021 Christian Neumanns, email: chris@pml-lang.dev

factory AST_node_attributes

    attributes access:private
        map mutable_map<key:string, value:AST_node_attribute>
    .

    functions

        is_empty = a_map.is_empty

        contains = a_map.contains_key ( attribute.name.full_name )

        add

            a_map.add (
                key = attribute.name.full_name
                value = attribute )
        .

        add_or_replace

            a_map.add_or_replace (
                key = attribute.name.full_name
                value = attribute )
        .

        get_list = a_map.value_stream.to_list_or_null

        get_string_map

            if a_map.is_empty
                return null
            .

            const r = mutable_map_with_nulls<key:string value:string or null>.create

            repeat for each value attribute in a_map
                r.add (
                    key = attribute.name.full_name
                    value = attribute.value.text )
            .

            return r.make_immutable
        .

        get_or_null = a_map.get_or_null ( i_name )

        to_string

            const size = a_map.size
            return size.to_string & if size =v 1 then " attribute" else " attributes"
        .
    .

    creator create kind:in_all

        this.map = mutable_map<key:string, value:AST_node_attribute>.create

        return this
    .
.
